plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.4.1' apply false
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'org.omer'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    // Log4j2 Core
    api 'org.apache.logging.log4j:log4j-api:2.24.3'
    api 'org.apache.logging.log4j:log4j-core:2.24.3'
    api 'org.apache.logging.log4j:log4j-slf4j2-impl:2.24.3'
    api 'org.apache.logging.log4j:log4j-spring-boot:2.24.3'

    // LMAX Disruptor for async logging
    api 'com.lmax:disruptor:3.4.4'

    // YAML configuration support
    runtimeOnly 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'

    // Spring Web for Filter support (optional dependency for consumers)
    compileOnly 'org.springframework:spring-web'
    compileOnly 'jakarta.servlet:jakarta.servlet-api'

    // Spring Boot auto-configuration support
    compileOnly 'org.springframework.boot:spring-boot-autoconfigure'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation(platform('org.junit:junit-bom:5.10.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
    
    // Lombok coverage in tests
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
}

test {
    useJUnitPlatform()
}
